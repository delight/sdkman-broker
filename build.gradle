import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:1.3.3"
    classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    classpath "com.bmuschko:gradle-docker-plugin:3.0.1"
  }
}

apply plugin: "io.ratpack.ratpack-java"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "com.bmuschko.docker-remote-api"
apply plugin: "idea"
apply plugin: "groovy"

repositories {
  jcenter()
}

dependencies {
  runtime "org.slf4j:slf4j-simple:1.7.12"
  compile ratpack.dependency("guice")
  compile "org.mongodb:mongo-java-driver:3.2.2"
  testCompile "org.codehaus.groovy:groovy-all:2.4.7"
  testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
  testCompile "info.cukes:cucumber-junit:1.1.8"
  testCompile "info.cukes:cucumber-groovy:1.1.8"
  testCompile "com.github.groovy-wslite:groovy-wslite:0.8.0"
}

mainClassName = "io.sdkman.broker.Main"

configurations {
  funTestCompile.extendsFrom testCompile
  funTestRuntime.extendsFrom testRuntime
}

sourceSets {
  funtest {
    groovy.srcDir file("src/funtest/groovy")
    resources.srcDir     file("src/funtest/resources")
    compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.funTestCompile
    runtimeClasspath = output + compileClasspath + configurations.funTestRuntime
  }
}

assemble.dependsOn shadowJar

docker {
  url = "unix:///var/run/docker.sock"
}

task pullDbImage(type: DockerPullImage) {
  repository = "mongo"
  tag = "3.2"
}

task createDbContainer(type: DockerCreateContainer) {
  dependsOn pullDbImage
  containerName = "mongo"
  imageId = "${pullDbImage.repository}:${pullDbImage.tag}"
  portBindings = ["27017:27017"]
  exposePorts("tcp", [27017])
}

task startDbContainer(type: DockerStartContainer) {
  dependsOn createDbContainer
  targetContainerId { createDbContainer.containerId }
}

task stopDbContainer(type: DockerStopContainer) {
  targetContainerId { createDbContainer.containerId }
}

task removeDbContainer(type: DockerRemoveContainer) {
  dependsOn stopDbContainer
  targetContainerId { createDbContainer.containerId }
}

task buildAppImage(type: DockerBuildImage) {
  dependsOn assemble
  inputDir = file(".")
  tag = "sdkman/sdkman-broker:latest"
}

task createAppContainer(type: DockerCreateContainer) {
  containerName = "sdkman_broker"
  dependsOn buildAppImage, startDbContainer
  targetImageId { buildAppImage.imageId }
  portBindings = ["5050:5050"]
  exposePorts("tcp", [5050])
  links = ["mongo:mongo"]
}

task startAppContainer(type: DockerStartContainer) {
  dependsOn createAppContainer
  targetContainerId { createAppContainer.containerId }
}

task stopAppContainer(type: DockerStopContainer) {
  targetContainerId { createAppContainer.containerId }
}

task removeAppContainer(type: DockerRemoveContainer) {
  dependsOn stopAppContainer
  targetContainerId { createAppContainer.containerId }
}

task funtest(type: Test) {
  testClassesDir = sourceSets.funtest.output.classesDir
  classpath = sourceSets.funtest.runtimeClasspath
  dependsOn startAppContainer
  finalizedBy removeAppContainer, removeDbContainer
}

check.dependsOn funtest
